# By Mi GAO. 29/07/2018.
# Execution begins at address 0
		.pos 0
		irmovq stack, %rsp		# Set up stack pointer
		call main				# Execute main program
		halt					# Terminate program
		
# Source block
	.align 8
src:
		.quad 0x00c
		.quad 0x0b0
		.quad 0xa00
dest:
		.quad 0x111
		.quad 0x222
		.quad 0x333

main:
		irmovq src, %rdi
		irmovq dest, %rsi
		irmovq $3, %rdx
		call copy
		ret

# long copy_block(long *src, long *dest, long len)
# start in %rdi
copy:
		irmovq $8, %r8
		irmovq $1, %r10
		xorq %rax, %rax
		andq %rdx, %rdx			# Set CC
		jmp test
loop:
		mrmovq (%rdi), %r9
		addq %r8, %rdi
		rmmovq %r9, (%rsi)
		addq %r8, %rsi
		xorq %r9, %rax
		subq %r10, %rdx
test:
		jg loop
		ret
		
# Stack start here and grows to lower addresses
	.pos 0x200
stack:
		
